group 'coolcrowd'
version '0.5'

apply plugin: 'java'
apply plugin: 'nu.studer.jooq'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'application'
apply plugin: "io.ratpack.ratpack-java"

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'mysql:mysql-connector-java:5.+'
        classpath 'nu.studer:gradle-jooq-plugin:1.0.5'
        classpath "net.saliman:gradle-cobertura-plugin:2.3.+"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.+"
        classpath "io.ratpack:ratpack-gradle:1.2.0"
    }
}

mainClassName = 'edu.kit.ipd.crowdcontrol.workerservice.Main'

jar {
    manifest {
        attributes 'Main-Class': 'edu.kit.ipd.crowdcontrol.workerservice.Main'
    }
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'edu.kit.ipd.crowdcontrol.workerservice.Main'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jooq:jooq:3.+'
    compile 'org.jooq:jooq-meta:3.+'
    compile 'org.jooq:jool:0.9.+'
    compile 'mysql:mysql-connector-java:5.+'
    compile 'org.mybatis:mybatis:3.+'
    compile 'com.mchange:c3p0:0.9.5.2'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.protobuf:protobuf-java:3.+'
    compile 'com.google.protobuf:protobuf-java-util:3.+'
    compile 'commons-validator:commons-validator:1.5.0'
    compile 'com.mashape.unirest:unirest-java:1.4.7'
    compile 'org.apache.commons:commons-io:1.3.+'
    compile 'ch.qos.logback:logback-core:1.+'
    compile 'ch.qos.logback:logback-classic:1.+'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'io.jsonwebtoken:jjwt:0.6.+'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'commons-io:commons-io:2.+'
    testCompile ratpack.dependency('test')
}

applicationDefaultJvmArgs = [
        "-Dworkerservice.config.file=./etc/workerservice/configuration.properties",
        "-Dlogback.configurationFile=./etc/workerservice/logging.xml"]

compileJava {
    options.encoding = "UTF-8"
}

//tests

test {
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
        events 'failed', 'skipped', 'passed'
    }
}

sourceSets {
    unitTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/java')
        }

        resources.srcDir file('src/test/resources')
    }

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    jacoco {
        destinationFile = file("$buildDir/jacoco/integrationTest.exec")
        classDumpFile = file("$buildDir/classes/integrationTest")
    }

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    testLogging {
        events 'failed', 'skipped', 'passed'
    }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.7.5.201505241946"
}

jacocoTestReport {
    executionData = fileTree(dir: 'build/jacoco', include: '**/*.exec')

    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['edu/kit/ipd/crowdcontrol/objectservice/proto/**',
                              'edu/kit/ipd/crowdcontrol/workerservice/proto/**',
                              'edu/kit/ipd/crowdcontrol/workerservice/database/model/**'])
        })
    }
}


//database-model
jooq {
    sample(sourceSets.main) {
        jdbc {
            url = 'jdbc:mysql://localhost:8888'
            driver = 'com.mysql.jdbc.Driver'
            user = 'LeanderK'
            password = ''
            schema = 'crowdcontrol'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.mysql.MySQLDatabase'
                inputSchema = 'crowdcontrol'
            }
            generate {
                daos = true
            }
            target {
                packageName = "edu.kit.ipd.crowdcontrol.workerservice.database.model"
                directory = 'src/main/java'
            }
        }
    }
}

generateSampleJooqSchemaSource.enabled = false

//docker-related
task prepareDocker(type: Copy) {
    dependsOn "fatJar"
    file("./bin").mkdirs()
    from "$buildDir/libs/" + project.name + '-all-' + project.version + ".jar"
    into "./bin"
    rename(project.name + '-all-' + project.version + ".jar", project.name + '.jar')
    doLast {
        clean.execute()
    }
}
